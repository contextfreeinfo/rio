#foreign(header = "<errno.h>")
#foreign(header = "<io.h>")
// TODO: For some reason _finddata_t alone doesn't work. I have to be explicit.
// #foreign(preamble = "typedef struct _finddata_t FindData;")
#foreign(preamble = "typedef struct _finddata64i32_t FindData;")

@foreign
const _A_SUBDIR = 0x10;

@foreign
const ENOENT = 3025;

@foreign
struct FindData {
  attrib: uint;
  size: uint32;
  name: [MAX_PATH]char;
}

@foreign
def _findclose(handle: intptr) -> int;

@foreign
def _findfirst(filespec: *const char, fileinfo: &FindData) -> intptr;

@foreign
def _findnext(handle: intptr, fileinfo: &FindData) -> int;

@foreign
def _fullpath(absPath: *char, relPath: *const char, maxLength: usize) -> *char;

@foreign("_strdup")
def strdup(strSource: *const char) -> *char;

def path_absolute(path: [MAX_PATH]char) {
  let mut rel_path: [MAX_PATH]char;
  path_copy(rel_path, path);
  _fullpath(path, rel_path, MAX_PATH);
}

def dir_list_free(iter: &DirListIter) {
  if (iter.valid) {
    _findclose(iter.handle as intptr);
    iter.valid = false;
    iter.error = false;
  }
}

def dir__update(iter: &DirListIter, done: bool, fileinfo: &FindData) {
  iter.valid = !done;
  iter.error = done && errno != ENOENT;
  if (!done) {
    iter.size = fileinfo.size;
    memcpy(iter.name, fileinfo.name, sizeof(iter.name) - 1);
    iter.name[MAX_PATH - 1] = 0;
    iter.is_dir = fileinfo.attrib & _A_SUBDIR;
  }
}

def dir_list_next(iter: &DirListIter) {
  if (!iter.valid) {
    return;
  }
  do {
    let mut fileinfo: FindData;
    let mut result = _findnext(iter.handle as intptr, &fileinfo);
    dir__update(iter, result != 0, &fileinfo);
    if (result != 0) {
      dir_list_free(iter);
      return;
    }
  } while (dir_excluded(iter));
}

def dir_list(iter: &DirListIter, path: *const char) {
  memset(iter, 0, sizeof(*iter));
  path_copy(iter.base, path);
  let mut filespec: [MAX_PATH]char;
  path_copy(filespec, path);
  path_join(filespec, "*");
  let mut fileinfo: FindData;
  let mut handle = _findfirst(filespec, &fileinfo);
  iter.handle = handle as &void;
  dir__update(iter, handle == -1, &fileinfo);
  if (dir_excluded(iter)) {
    dir_list_next(iter);
  }
}
