typedef MapClosureCall = do(self: &void, item: Any) -> &void;

struct MapClosure {
  self: &void;
  call: MapClosureCall;
}

struct TypeMap {
  type_args: Slice<TypeArg>;
  type_params: Slice<Decl>;
}

def dupe_aggregate(aggregate: &Aggregate, map: &MapClosure) -> &Aggregate {
  let dupe: &Aggregate = map.call(map.self, (aggregate, typeof(*aggregate)));
  if (dupe) {
    return dupe;
  }
  dupe = ast_dup.<Aggregate>(aggregate);
  dupe.items = ast_dup_slice.<AggregateItem>(dupe.items);
  for &(dupe.items) do(item) {
    // These are value, not pointer, so presume any modifications are in place.
    map.call(map.self, (item, typeof(*item)));
    // Now go deeper.
    @complete
    switch (item.kind) {
      Field => {
        item.type = dupe_typespec(item.type, map);
      }
      Subaggregate => {
        item.subaggregate = dupe_aggregate(item.subaggregate, map);
      }
    }
  }
  return dupe;
}

def dupe_block(block: StmtList, map: &MapClosure) -> StmtList {
  let dupe = &block;
  dupe.stmts = ast_dup_slice.<&Stmt>(dupe.stmts);
  for &(dupe.stmts) do(stmt) {
    *stmt = dupe_stmt(*stmt, map);
  }
  return *dupe;
}

def dupe_expr(expr: &Expr, map: &MapClosure) -> &Expr {
  if (!expr) {
    return NULL;
  }
  let dupe: &Expr = ast_dup.<Expr>(expr);
  @complete
  switch (dupe.kind) {
    AlignofExpr, SizeofExpr, TypeofExpr, Paren => {
      dupe.arg = dupe_expr(dupe.arg, map);
    }
    AlignofType, SizeofType, TypeofType => {
      dupe.type_arg = dupe_typespec(dupe.type_arg, map);
    }
    Binary => {
      dupe.binary.left = dupe_expr(dupe.binary.left, map);
      dupe.binary.right = dupe_expr(dupe.binary.right, map);
    }
    Call => {
      let call = &dupe.call;
      call.expr = dupe_expr(call.expr, map);
      call.args = dupe_fields(call.args, map);
    }
    Cast => {
      dupe.cast.type = dupe_typespec(dupe.cast.type, map);
      dupe.cast.expr = dupe_expr(dupe.cast.expr, map);
    }
    Compound => {
      let compound = &dupe.compound;
      compound.type = dupe_typespec(compound.type, map);
      compound.fields = dupe_fields(compound.fields, map);
    }
    Field => {
      dupe.field.expr = dupe_expr(dupe.field.expr, map);
      // TODO type_args
    }
    Float, Int, Str => {
      // Already good.
    }
    Index => {
      dupe.index.expr = dupe_expr(dupe.index.expr, map);
      dupe.index.index = dupe_expr(dupe.index.index, map);
    }
    Modify => {
      dupe.modify.expr = dupe_expr(dupe.modify.expr, map);
    }
    Name => {
      // TODO A name could also reference type params.
      // let old = dupe.type_args.items;
      dupe.type_args = dupe_type_args(dupe.type_args, map);
      // if (dupe.type_args.items != old) {
      //   printf("Duping type arg: was %s, now %s\n", get_typespec_sym_name(old[0].val), get_typespec_sym_name(dupe.type_args[0].val));
      // }
    }
    Offsetof => {
      let offsetof_field = &dupe.offsetof_field;
      offsetof_field.type = dupe_typespec(offsetof_field.type, map);
    }
    Unary => {
      dupe.unary.expr = dupe_expr(dupe.unary.expr, map);
    }
    default => {
      printf("Unhandled: %d\n", dupe.kind);
      #assert(false);
      return NULL;
    }
  }
  return dupe;
}

def dupe_fields(
  fields: Slice<CompoundField>, map: &MapClosure,
) -> Slice<CompoundField> {
  let dupe = ast_dup_slice.<CompoundField>(fields);
  for &(dupe) do(field) {
    field.val = dupe_expr(field.val, map);
    @complete
    switch (field.kind) {
      Index => {
        field.index = dupe_expr(field.index, map);
      }
      Default, Name => {
        // Nothing to do.
      }
    }
  }
  return dupe;
}

def dupe_function(func: DeclFunc, shallow: bool, map: &MapClosure) -> DeclFunc {
  dupe_function_fields(&func, shallow, map);
  return func;
}

def dupe_function_fields(dupe: &DeclFunc, shallow: bool, map: &MapClosure) {
  dupe.params = ast_dup_slice.<FuncParam>(dupe.params);
  for &(dupe.params) do(param) {
    *param = dupe_func_param(*param, map);
  }
  dupe.ret_type = dupe_typespec(dupe.ret_type, map);
  if (!shallow) {
    dupe.block = dupe_block(dupe.block, map);
  }
}

def dupe_func_param(param: FuncParam, map: &MapClosure) -> FuncParam {
  let dupe = &param;
  dupe.type = dupe_typespec(dupe.type, map);
  if (dupe.default_val) {
    dupe.default_val = dupe_expr(dupe.default_val, map);
  }
  return *dupe;
}

def dupe_stmt(stmt: &Stmt, map: &MapClosure) -> &Stmt {
  if (!stmt) {
    return NULL;
  }
  let dupe: &Stmt = ast_dup.<Stmt>(stmt);
  // Let notes (?) and pos alone, but look into statement content.
  @complete
  switch (dupe.kind) {
    Assign => {
      let assign = &stmt.assign;
      assign.left = dupe_expr(assign.left, map);
      assign.right = dupe_expr(assign.right, map);
    }
    Block => {
      dupe.block = dupe_block(dupe.block, map);
    }
    Break, Close, Continue, Goto, Label, Note => {
      // Keep untouched.
      // Might be good to avoid having duped in the first place, but we likely
      // have more costs elsewhere, like needlessly duping whole blocks.
    }
    DoWhile, While => {
      let while_stmt = &dupe.while_stmt;
      while_stmt.cond = dupe_expr(while_stmt.cond, map);
      while_stmt.block = dupe_block(while_stmt.block, map);
    }
    Expr, Return => {
      dupe.expr = dupe_expr(dupe.expr, map);
    }
    For => {
      let for_stmt = &dupe.for_stmt;
      for_stmt.init = dupe_stmt(for_stmt.init, map);
      for_stmt.cond = dupe_expr(for_stmt.cond, map);
      for_stmt.next = dupe_stmt(for_stmt.next, map);
      for_stmt.block = dupe_block(for_stmt.block, map);
    }
    ForEach => {
      let for_each = &dupe.for_each;
      for_each.expr = dupe_expr(for_each.expr, map);
      dupe_function_fields(&for_each.func, false, map);
      // The length_type is to be filled in during resolution.
    }
    If => {
      let if_stmt = &dupe.if_stmt;
      if_stmt.init = dupe_stmt(if_stmt.init, map);
      if_stmt.cond = dupe_expr(if_stmt.cond, map);
      if_stmt.then_block = dupe_block(if_stmt.then_block, map);
      if_stmt.elseifs = ast_dup_slice.<ElseIf>(if_stmt.elseifs);
      for &(if_stmt.elseifs) do(elseif) {
        elseif.cond = dupe_expr(elseif.cond, map);
        elseif.block = dupe_block(elseif.block, map);
      }
      if_stmt.else_block = dupe_block(if_stmt.else_block, map);
    }
    Init => {
      let init = &dupe.init;
      init.type = dupe_typespec(init.type, map);
      init.expr = dupe_expr(init.expr, map);
      // printf("Totally on %d: %p\n", stmt.pos.line, dupe.init.type);
    }
    Switch => {
      let switch_stmt = &dupe.switch_stmt;
      switch_stmt.expr = dupe_expr(switch_stmt.expr, map);
      switch_stmt.cases = ast_dup_slice.<SwitchCase>(switch_stmt.cases);
      for &(switch_stmt.cases) do(case_) {
        case_.patterns = ast_dup_slice.<SwitchCasePattern>(case_.patterns);
        for &(case_.patterns) do(pattern) {
          pattern.start = dupe_expr(pattern.start, map);
          pattern.end = dupe_expr(pattern.end, map);
        }
        case_.block = dupe_block(case_.block, map);
      }
    }
  }
  return dupe;
}

def dupe_type_args(
  type_args: Slice<TypeArg>, map: &MapClosure,
) -> Slice<TypeArg> {
  if (type_args.length) {
    type_args = ast_dup_slice.<TypeArg>(type_args);
    for &(type_args) do(type_arg) {
      type_arg.val = dupe_typespec(type_arg.val, map);
    }
  }
  return type_args;
}

def dupe_typespec(type: &Typespec, map: &MapClosure) -> &Typespec {
  if (!type) {
    return NULL;
  }
  let dupe = map.call(map.self, (type, typeof(*type))) as &Typespec;
  if (dupe) {
    return dupe;
  }
  dupe = ast_dup.<Typespec>(type);
  @complete
  switch (type.kind) {
    Array, Const, Ptr, Ref => {
      dupe.base = dupe_typespec(type.base, map);
    }
    Func => {
      fatal_error(type.pos, "Function types not yet supported in generics\n");
    }
    Name => {
      dupe.names = ast_dup_slice.<TypespecName>(dupe.names);
      for &(dupe.names) do(type_name) {
        type_name.type_args = dupe_type_args(type_name.type_args, map);
      }
    }
  }
  return dupe;
}

def find_generic(type: &Typespec) -> bool {
  if (!type) {
    return false;
  }
  @complete
  switch (type.kind) {
    Array, Const, Ptr, Ref => {
      return find_generic(type.base);
    }
    Func => {
      for (type.function.args) do(arg) {
        if (find_generic(arg)) {
          return true;
        }
      }
      return find_generic(type.function.ret);
    }
    Name => {
      if (type.decl && type.decl.is_generic) {
        return true;
      }
      for &(type.names) do(type_name) {
        for &(type_name.type_args) do(type_arg) {
          if (find_generic(type_arg.val)) {
            return true;
          }
        }
      }
    }
  } switch;
  return false;
}

def get_typespec_sym_name(type: &Typespec) -> own *char {
  let buf: *char;
  put_typespec_sym_name(&buf, type);
  return buf;
}

def map_type_args(self: &TypeMap, item: Any) -> &void {
  switch (item.type) {
    typeof(Aggregate) => {
      // Let these be copied so we can make changes inside.
      return NULL;
    }
    typeof(Typespec) => {
      let type: &Typespec = item.ptr;
      switch (type.kind) {
        Typespec.Name => {
          // See if this is a reference to a type parameter.
          let decl = type.decl;
          let params = self.type_params;
          // let verbose = false;
          // if (!strcmp(type.names[0].name, "Numb")) {
          //   verbose = true;
          //   printf("Found Numb in map\n");
          // }
          for &(params) do(param, i) {
            if (decl == param) {
              // Yep. Found it.
              // TODO Need to deal with type args of parameterized types ...
              let arg = &self.type_args[i];
              // if (verbose) {printf("Still found it: %p\n", arg.val);}
              return arg.val;
            }
          }
          // Nope. Just let it be copied in case of type args.
          // if (verbose) {printf("Not there\n");}
          return NULL;
        }
        default => {
          // Let others be copied so we can make changes inside.
          return NULL;
        }
      }
    }
    default => {
      // Let these move on.
      return item.ptr;
    }
  }
}

def put_typespec_sym_name(buf: **char, type: &Typespec) {
  @complete
  switch (type.kind) {
    Name => {
      // This is the perceived type, which is what we want for mangling, so the
      // programmer has some manual control.
      for &(type.names) do(name, i) {
        if (i) {
          buf_printf(buf, "_");
        }
        buf_printf(buf, "%s", name.name);
        // And args, too.
        for &(name.type_args) do(type_arg) {
          buf_printf(buf, "_");
          put_typespec_sym_name(buf, type_arg.val);
        }
      }
    }
    Const => {
      buf_printf(buf, "const_");
      put_typespec_sym_name(buf, type.base);
    }
    Ptr => {
      buf_printf(buf, "ptr_");
      put_typespec_sym_name(buf, type.base);
    }
    Ref => {
      buf_printf(buf, "ref_");
      put_typespec_sym_name(buf, type.base);
    }
    Array => {
      // TODO Do want to resolve the constant count or to convert the expression
      // TODO to a string, or just force typedefs????
      // buf_printf(buf, "array%zu_", type.num_elems);
      buf_printf(buf, "array_");
      put_typespec_sym_name(buf, type.base);
    }
    Func => {
      let function = &type.function;
      buf_printf(buf, "do_");
      for (type.function.args) do(arg, i) {
        if (i) {
          buf_printf(buf, "_");
        }
        put_typespec_sym_name(buf, arg);
      }
      if (type.function.has_varargs) {
        buf_printf(buf, "_etc");
      }
      if (type.function.ret) {
        buf_printf(buf, "_to_");
        put_typespec_sym_name(buf, type.function.ret);
      }
    }
  } switch;
}
