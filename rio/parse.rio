def parse_type_func_param() -> &Typespec {
  let mut type = parse_type();
  if (match_token(TokenKind.Colon)) {
    if (type.kind != Typespec.Name) {
      error(
        token.pos,
        "Colons in parameters of function types must be preceded by names.",
      );
    }
    type = parse_type();
  }
  return type;
}

def parse_type_func() -> &Typespec {
  let mut pos = token.pos;
  let mut args: *&Typespec = NULL;
  let mut has_varargs: bool = false;
  expect_token(TokenKind.Lparen);
  while (!is_token(TokenKind.Rparen)) {
    if (match_token(TokenKind.Ellipsis)) {
      if (has_varargs) {
        error(token.pos, "Multiple ellipsis instances in function type");
      }
      has_varargs = true;
    } else {
      if (has_varargs) {
        error(token.pos, "Ellipsis must be last parameter in function type");
      }
      let mut param = parse_type_func_param();
      buf_push(&args as &&void, &param, sizeof(param));
    }
    if (!match_token(TokenKind.Comma)) {
      break;
    }
  } while;
  expect_token(TokenKind.Rparen);
  let mut ret: &Typespec = NULL;
  if (match_token(TokenKind.Arrow)) {
    ret = parse_type();
  }
  return new_typespec_func(pos, (args, buf_len(args)), ret, has_varargs);
}

def parse_type_args() -> Slice<TypeArg> {
  let mut args: *TypeArg;
  if (match_token(TokenKind.Lt)) {
    do {
      let mut type_arg = TypeArg{token.pos, val: parse_type()};
      buf_push(&args as &&void, &type_arg, sizeof(*args));
    } while (match_token(TokenKind.Comma));
    if (is_token(TokenKind.Rshift)) {
      // Abusive change of current token.
      token.kind = TokenKind.Gt;
    } else {
      expect_token(TokenKind.Gt);
    }
  }
  return {items: args, length: buf_len(args)};
}

def parse_type_base() -> &Typespec {
  if (is_token(TokenKind.Name)) {
    let mut pos = token.pos;
    let mut names: *TypespecName;
    let mut token_name = token.name;
    next_token();
    let mut first_name =
      TypespecName{name: token_name, type_args: parse_type_args()};
    buf_push(&names as &&void, &first_name, sizeof(first_name));
    while (match_token(TokenKind.Dot)) {
      let mut name =
        TypespecName{name: parse_name(), type_args: parse_type_args()};
      buf_push(&names as &&void, &name, sizeof(name));
    }
    let mut result = new_typespec_name(pos, (names, buf_len(names)));
    buf_free(&names as &&void);
    return result;
  } else if (match_keyword(do_keyword)) {
    return parse_type_func();
  } else if (match_token(TokenKind.Lparen)) {
    let mut type = parse_type();
    expect_token(TokenKind.Rparen);
    return type;
  } else {
    fatal_error(token.pos, "Unexpected token %s in type", token_info());
    return NULL;
  }
}

def parse_type() -> &Typespec {
  let mut pos = token.pos;
  let mut type: &Typespec;
  // Check modifiers.
  if (match_token(TokenKind.Lbracket)) {
    let mut size: &Expr = NULL;
    if (!is_token(TokenKind.Rbracket)) {
      size = parse_expr();
    }
    expect_token(TokenKind.Rbracket);
    type = new_typespec_array(pos, parse_type(), size);
  } else if (match_token(TokenKind.And)) {
    type = new_typespec_ref(pos, parse_type(), false);
  } else if (match_token(TokenKind.AndAnd)) {
    // Special double ref handling for &&, so the tokenizer can stay consistent.
    type = new_typespec_ref(pos, parse_type(), false);
    type = new_typespec_ref(pos, type, false);
  } else if (match_token(TokenKind.Mul)) {
    type = new_typespec_ptr(pos, parse_type(), false);
  } else if (match_keyword(const_keyword)) {
    type = new_typespec_const(pos, parse_type());
  } else if (match_keyword(own_keyword)) {
    // TODO Track owning.
    type = parse_type();
  } else {
    type = parse_type_base();
  }
  return type;
}

def parse_expr_struct_field(is_tuple: bool) -> CompoundField {
  let mut pos = token.pos;
  let mut expr = parse_expr();
  if (match_token(TokenKind.Colon)) {
    if (expr.kind != Expr.Name) {
      // TODO Allow string literals???
      // TODO Allow other expressions in case compile-time constant strings?
      fatal_error(token.pos, "Field key not a name");
      return {};
    }
    return CompoundField.Name{expr: parse_expr(), expr.name, pos};
  } else {
    if (is_tuple) {
      // Positional.
      // Validate later the issue of positional after named.
      return CompoundField.Default{expr, pos};
    } else {
      // Possibly implied name.
      let mut name = find_expr_name(pos, expr);
      return CompoundField.Name{expr, name, pos};
    }
  }
}

def find_expr_name(pos: SrcPos, expr: &Expr) -> *const char {
  switch (expr.kind) {
    Field => {
      return expr.field.name;
    }
    Name => {
      return expr.name;
    }
    Unary => {
      if (expr.unary.op == TokenKind.And) {
        return find_expr_name(pos, expr.unary.expr);
      }
    }
  }
  fatal_error(pos, "No implied name");
  return NULL;
}

def parse_expr_struct(type: &Typespec) -> &Expr {
  let mut pos = token.pos;
  expect_token(TokenKind.Lbrace);
  let mut fields: *CompoundField;
  while (!is_token(TokenKind.Rbrace)) {
    let mut field = parse_expr_struct_field(false);
    buf_push(&fields as &&void, &field, sizeof(field));
    if (!match_token(TokenKind.Comma)) {
      break;
    }
  }
  expect_token(TokenKind.Rbrace);
  return new_expr_compound(
    pos, ExprCompound.Struct, type, (fields, buf_len(fields)),
  );
}

def parse_expr_tuple(type: &Typespec) -> &Expr {
  let mut pos = token.pos;
  expect_token(TokenKind.Lparen);
  let mut fields: *CompoundField;
  while (!is_token(TokenKind.Rparen)) {
    let mut field = parse_expr_struct_field(true);
    if (
      !fields &&
      field.kind == CompoundField.Default &&
      match_token(TokenKind.Rparen)
    ) {
      // First field had nothing special, so just parens.
      return new_expr_paren(pos, field.expr);
    }
    buf_push(&fields as &&void, &field, sizeof(field));
    if (!match_token(TokenKind.Comma)) {
      break;
    }
  }
  expect_token(TokenKind.Rparen);
  return new_expr_compound(
    pos, ExprCompound.Tuple, type, (fields, buf_len(fields)),
  );
}

def parse_expr_list_item() -> CompoundField {
  let mut pos = token.pos;
  let mut expr = parse_expr();
  if (match_token(TokenKind.Colon)) {
    switch (expr.kind) {
      Compound => {
        if (expr.compound.kind == ExprCompound.List) {
          // Allow square brackets around indices, to match structs and js.
          // TODO Really?
          let mut fields = expr.compound.fields;
          if (fields.length != 1) {
            fatal_error(token.pos, "Computed field key must be single");
          }
          warning(pos, "Needless brackets");
          expr = fields[0].expr;
        }
      }
      Name => {
        return CompoundField.Name{expr: parse_expr(), expr.name, pos};
      }
    }
    // Any other errors in expression type can be resolved later.
    return CompoundField.Index{expr: parse_expr(), index: expr, pos};
    // return Index{expr: parse_expr(), index: expr, pos};
  } else {
    return CompoundField.Default{expr, pos};
  }
}

def parse_expr_list(type: &Typespec) -> &Expr {
  let mut pos = token.pos;
  expect_token(TokenKind.Lbracket);
  let mut fields: *CompoundField = NULL;
  while (!is_token(TokenKind.Rbracket)) {
    let mut field = parse_expr_list_item();
    buf_push(&fields as &&void, &field, sizeof(field));
    if (!match_token(TokenKind.Comma)) {
      break;
    }
  }
  expect_token(TokenKind.Rbracket);
  return new_expr_compound(
    pos, ExprCompound.List, type, (fields, buf_len(fields)),
  );
}

def parse_expr_operand() -> &Expr {
  let mut pos = token.pos;
  if (is_token(TokenKind.Int)) {
    let mut val = token.int_val;
    let mut mod = token.mod;
    let mut suffix = token.suffix;
    next_token();
    return new_expr_int(pos, val, mod, suffix);
  } else if (is_token(TokenKind.Float)) {
    let mut start = token.start;
    let mut end = token.end;
    let mut val = token.float_val;
    let mut suffix = token.suffix;
    next_token();
    return new_expr_float(pos, start, end, val, suffix);
  } else if (is_token(TokenKind.Str)) {
    let mut val = token.str_val;
    let mut mod = token.mod;
    next_token();
    return new_expr_str(pos, val, mod);
  } else if (is_token(TokenKind.Name)) {
    let mut name = token.name;
    next_token();
    if (is_token(TokenKind.Lbrace)) {
      return parse_expr_struct(new_typespec_name1(pos, name));
    } else {
      return new_expr_name(pos, name);
    }
  } else if (match_keyword(sizeof_keyword)) {
    if (match_token(TokenKind.Lparen)) {
      let mut expr = parse_expr();
      expect_token(TokenKind.Rparen);
      return new_expr_sizeof_expr(pos, expr);
    } else {
      expect_token(TokenKind.Lt);
      let mut type = parse_type();
      expect_close_angle();
      return new_expr_sizeof_type(pos, type);
    }
  } else if (match_keyword(alignof_keyword)) {
    if (match_token(TokenKind.Lparen)) {
      let mut expr = parse_expr();
      expect_token(TokenKind.Rparen);
      return new_expr_alignof_expr(pos, expr);
    } else {
      expect_token(TokenKind.Lt);
      let mut type = parse_type();
      expect_close_angle();
      return new_expr_alignof_type(pos, type);
    }
  } else if (match_keyword(typeof_keyword)) {
    if (match_token(TokenKind.Lparen)) {
      let mut expr = parse_expr();
      expect_token(TokenKind.Rparen);
      return new_expr_typeof_expr(pos, expr);
    } else {
      expect_token(TokenKind.Lt);
      let mut type = parse_type();
      expect_close_angle();
      return new_expr_typeof_type(pos, type);
    }
  } else if (match_keyword(offsetof_keyword)) {
    expect_token(TokenKind.Lparen);
    let mut type = parse_type();
    expect_token(TokenKind.Comma);
    let mut name = parse_name();
    expect_token(TokenKind.Rparen);
    return new_expr_offsetof(pos, type, name);
  } else if (is_token(TokenKind.Lbracket)) {
    return parse_expr_list(NULL);
  } else if (is_token(TokenKind.Lbrace)) {
    return parse_expr_struct(NULL);
  } else if (match_token(TokenKind.Lt)) {
    let mut type = parse_type();
    expect_close_angle();
    if (is_token(TokenKind.Lbrace)) {
      return parse_expr_struct(type);
    } else if (is_token(TokenKind.Lparen)) {
      return parse_expr_tuple(type);
    } else if (is_token(TokenKind.Lbracket)) {
      return parse_expr_list(type);
    } else {
      return new_expr_cast(pos, type, parse_expr_unary());
    }
  } else if (is_token(TokenKind.Lparen)) {
    return parse_expr_tuple(NULL);
  } else {
    fatal_error(token.pos, "Unexpected token %s in expression", token_info());
    return NULL;
  }
}

def parse_expr_base() -> &Expr {
  let mut expr = parse_expr_operand();
  while (
    is_token(TokenKind.Lparen) ||
    is_token(TokenKind.Lbrace) ||
    is_token(TokenKind.Lbracket) ||
    is_token(TokenKind.Dot) ||
    is_token(TokenKind.Inc) ||
    is_token(TokenKind.Dec)
  ) {
    let mut pos = token.pos;
    if (match_token(TokenKind.Lparen)) {
      let mut args: *CompoundField;
      // TODO Named args for the sake of tuples, at least.
      while (!is_token(TokenKind.Rparen)) {
        let mut arg = parse_expr_struct_field(true);
        buf_push(&args as &&void, &arg, sizeof(arg));
        if (!match_token(TokenKind.Comma)) {
          break;
        }
      }
      expect_token(TokenKind.Rparen);
      expr = new_expr_call(pos, expr, (args, buf_len(args)));
    } else if (is_token(TokenKind.Lbrace)) {
      switch (expr.kind) {
        Call => {
          // TODO Instead require `do {...}` for parsing as block???
          fatal_error(expr.pos, "No support yet for trailing blocks");
          return NULL;
        }
        default => {
          let mut compound = parse_expr_struct(NULL);
          let mut args = compound.compound.fields;
          compound.kind = Expr.Call;
          compound.call = {args, expr};
          expr = compound;
        }
      }
    } else if (match_token(TokenKind.Lbracket)) {
      let mut index = parse_expr();
      expect_token(TokenKind.Rbracket);
      expr = new_expr_index(pos, expr, index);
    } else if (is_token(TokenKind.Dot)) {
      next_token();
      if (is_token(TokenKind.Lt)) {
        // Generic type args.
        let mut type_args = parse_type_args();
        switch (expr.kind) {
          Field => expr.field.type_args = type_args;
          Name => expr.type_args = type_args;
          default => {
            fatal_error(pos, "Unexpected type args");
          }
        }
      } else {
        // Field access.
        let mut field = token.name;
        expect_token(TokenKind.Name);
        expr = new_expr_field(pos, expr, field);
      }
    } else {
      #assert(is_token(TokenKind.Inc) || is_token(TokenKind.Dec));
      let mut op = token.kind;
      next_token();
      expr = new_expr_modify(pos, op, true, expr);
    }
  } while;
  return expr;
}

def is_unary_op() -> bool {
  return
    is_token(TokenKind.Add) ||
    is_token(TokenKind.Sub) ||
    is_token(TokenKind.Mul) ||
    is_token(TokenKind.And) ||
    is_token(TokenKind.Neg) ||
    is_token(TokenKind.Not) ||
    is_token(TokenKind.Inc) ||
    is_token(TokenKind.Dec);
}

def parse_expr_unary() -> &Expr {
  if (is_unary_op()) {
    let mut pos = token.pos;
    let mut op = token.kind;
    next_token();
    if (op == TokenKind.Inc || op == TokenKind.Dec) {
      return new_expr_modify(pos, op, false, parse_expr_unary());
    } else {
      return new_expr_unary(pos, op, parse_expr_unary());
    }
  } else {
    return parse_expr_base();
  }
}

def is_mul_op() -> bool {
  return TokenKind.FirstMul <= token.kind && token.kind <= TokenKind.LastMul;
}

def parse_expr_as() -> &Expr {
  let mut expr = parse_expr_unary();
  while (is_token_name(as_name)) {
    let mut pos = token.pos;
    next_token();
    let mut type = parse_type();
    expr = new_expr_cast(pos, type, expr);
  }
  return expr;
}

def parse_expr_mul() -> &Expr {
  // TODO Insert parse_expr_range below this?
  // TODO Since & and | are set operators and range makes sets, & and | should
  // TODO operate on the results of ranges.
  let mut expr = parse_expr_as();
  while (is_mul_op()) {
    let mut pos = token.pos;
    let mut op = token.kind;
    next_token();
    expr = new_expr_binary(pos, op, expr, parse_expr_as());
  }
  return expr;
}

def is_add_op() -> bool {
  return TokenKind.FirstAdd <= token.kind && token.kind <= TokenKind.LastAdd;
}

def parse_expr_add() -> &Expr {
  let mut expr = parse_expr_mul();
  while (is_add_op()) {
    let mut pos = token.pos;
    let mut op = token.kind;
    next_token();
    expr = new_expr_binary(pos, op, expr, parse_expr_mul());
  }
  return expr;
}

def is_cmp_op() -> bool {
  return TokenKind.FirstCmp <= token.kind && token.kind <= TokenKind.LastCmp;
}

def parse_expr_cmp() -> &Expr {
  let mut expr = parse_expr_add();
  while (is_cmp_op()) {
    let mut pos = token.pos;
    let mut op = token.kind;
    next_token();
    expr = new_expr_binary(pos, op, expr, parse_expr_add());
  }
  return expr;
}

def parse_expr_and() -> &Expr {
  let mut expr = parse_expr_cmp();
  while (match_token(TokenKind.AndAnd)) {
    let mut pos = token.pos;
    expr = new_expr_binary(pos, TokenKind.AndAnd, expr, parse_expr_cmp());
  }
  return expr;
}

def parse_expr_or() -> &Expr {
  let mut expr = parse_expr_and();
  while (match_token(TokenKind.OrOr)) {
    let mut pos = token.pos;
    expr = new_expr_binary(pos, TokenKind.OrOr, expr, parse_expr_and());
  }
  return expr;
}

def parse_expr_ternary() -> &Expr {
  let mut pos = token.pos;
  let mut expr = parse_expr_or();
  if (match_token(TokenKind.Question)) {
    let mut then_expr = parse_expr_ternary();
    expect_token(TokenKind.Colon);
    let mut else_expr = parse_expr_ternary();
    expr = new_expr_ternary(pos, expr, then_expr, else_expr);
  }
  return expr;
}

def parse_expr() -> &Expr {
  return parse_expr_ternary();
}

def parse_paren_expr() -> &Expr {
  expect_token(TokenKind.Lparen);
  let mut expr = parse_expr();
  expect_token(TokenKind.Rparen);
  return expr;
}

def parse_stmt_block() -> StmtList {
  let mut pos = token.pos;
  expect_token(TokenKind.Lbrace);
  let mut stmts: *&Stmt = NULL;
  while (!is_token_eof() && !is_token(TokenKind.Rbrace)) {
    let mut stmt = parse_stmt();
    buf_push(&stmts as &&void, &stmt, sizeof(stmt));
  }
  expect_token(TokenKind.Rbrace);
  return new_stmt_list(pos, (stmts, buf_len(stmts)));
}

def parse_stmt_if(pos: SrcPos) -> &Stmt {
  expect_token(TokenKind.Lparen);
  let mut cond: &Expr;
  let mut init: &Stmt;
  if (match_keyword(let_keyword)) {
    init = parse_let_stmt(pos);
    if (match_token(TokenKind.Semicolon)) {
      cond = parse_expr();
    }
  } else {
    cond = parse_expr();
  }
  expect_token(TokenKind.Rparen);
  let mut then_block: StmtList = parse_stmt_block();
  let mut else_block: StmtList;
  let mut elseifs: *ElseIf;
  while (match_keyword(else_keyword)) {
    if (!match_keyword(if_keyword)) {
      else_block = parse_stmt_block();
      break;
    }
    let mut elseif_cond: &Expr = parse_paren_expr();
    let mut elseif_block: StmtList = parse_stmt_block();
    let mut elseif = ElseIf(elseif_cond, elseif_block);
    buf_push(&elseifs as &&void, &elseif, sizeof(elseif));
  } while;
  return new_stmt_if(
    pos, init, cond, then_block, (elseifs, buf_len(elseifs)), else_block,
  );
}

def parse_stmt_while(pos: SrcPos) -> &Stmt {
  let mut cond: &Expr = parse_paren_expr();
  return new_stmt_while(pos, cond, parse_stmt_block());
}

def parse_stmt_do_while(pos: SrcPos) -> &Stmt {
  let mut block: StmtList = parse_stmt_block();
  if (!match_keyword(while_keyword)) {
    fatal_error(token.pos, "Expected 'while' after 'do' block");
    return NULL;
  }
  let mut stmt: &Stmt = new_stmt_do_while(pos, parse_paren_expr(), block);
  expect_token(TokenKind.Semicolon);
  return stmt;
}

def is_assign_op() -> bool {
  return
    TokenKind.FirstAssign <= token.kind && token.kind <= TokenKind.LastAssign;
}

def parse_let_stmt(pos: SrcPos) -> &Stmt {
  let mut is_mut = match_keyword(mut_keyword);
  let mut name = parse_name();
  if (match_token(TokenKind.Assign)) {
    return new_stmt_init(pos, name, is_mut, NULL, parse_expr());
  } else if (match_token(TokenKind.Colon)) {
    let mut type = parse_type();
    let mut expr: &Expr;
    if (match_token(TokenKind.Assign)) {
      expr = parse_expr();
    }
    return new_stmt_init(pos, name, is_mut, type, expr);
  } else {
    fatal_error(token.pos, "Expected : or = after let mut, got %s", token_info());
    return NULL;
  }
}

def parse_simple_stmt() -> &Stmt {
  let mut pos = token.pos;
  let mut stmt: &Stmt;
  if (match_keyword(let_keyword)) {
    stmt = parse_let_stmt(pos);
  } else {
    let mut expr = parse_expr();
    if (expr.kind == Expr.Name && match_token(TokenKind.Colon)) {
      stmt = new_stmt_label(pos, expr.name);
    } else if (is_assign_op()) {
      let mut op = token.kind;
      next_token();
      stmt = new_stmt_assign(pos, op, expr, parse_expr());
    } else {
      stmt = new_stmt_expr(pos, expr);
    }
  }
  return stmt;
}

def parse_stmt_for_each(pos: SrcPos, get_ref: bool, expr: &Expr) -> &Stmt {
  let mut has_varargs: bool;
  let mut params: Slice<Member>;
  if (match_keyword(do_keyword)) {
    params = parse_decl_func_params(&has_varargs, true);
  }
  // Check varargs up front, since we don't even want to bother to record it.
  if (has_varargs) {
    fatal_error(pos, "Varargs not allowed in for-each loop");
  }
  return new_stmt_for_each(pos, get_ref, expr, params, parse_stmt_block());
}

def parse_stmt_for(pos: SrcPos) -> &Stmt {
  let mut get_ref = match_token(TokenKind.And);
  expect_token(TokenKind.Lparen);
  let mut init: &Stmt = NULL;
  if (!is_token(TokenKind.Semicolon)) {
    // TODO "For in" loops before we get back to generics.
    init = parse_simple_stmt();
  }
  if (init && init.kind == Stmt.Expr && is_token(TokenKind.Rparen)) {
    next_token();
    return parse_stmt_for_each(pos, get_ref, init.expr);
  } else if (get_ref) {
    fatal_error(pos, "Ref requested for segmented for loop");
  }
  expect_token(TokenKind.Semicolon);
  let mut cond: &Expr = NULL;
  if (!is_token(TokenKind.Semicolon)) {
    cond = parse_expr();
  }
  let mut next: &Stmt = NULL;
  if (match_token(TokenKind.Semicolon)) {
    if (!is_token(TokenKind.Rparen)) {
      next = parse_simple_stmt();
      if (next.kind == Stmt.Init) {
        error(token.pos, "Init statements not allowed in for-statement's next clause");
      }
    }
  } if;
  expect_token(TokenKind.Rparen);
  return new_stmt_for(pos, init, cond, next, parse_stmt_block());
}

def parse_switch_case_pattern() -> SwitchCasePattern {
  if (match_keyword(default_keyword)) {
    return SwitchCasePattern {is_default: true};
  }
  let mut start = parse_expr();
  let mut end: &Expr;
  if (match_token(TokenKind.Ellipsis)) {
    end = parse_expr();
  }
  return (false, start, end);
}

def parse_stmt_switch_case() -> SwitchCase {
  let mut patterns: *SwitchCasePattern;
  let mut pattern = parse_switch_case_pattern();
  buf_push(&patterns as &&void, &pattern, sizeof(pattern));
  // TODO Just parse expression and split on top-level TokenKind.Or.
  while (match_token(TokenKind.Comma)) {
    pattern = parse_switch_case_pattern();
    buf_push(&patterns as &&void, &pattern, sizeof(pattern));
  }
  expect_token(TokenKind.Spear);
  let mut pos = token.pos;
  let mut stmt = parse_stmt();
  return (
    // TODO Should the patterns be ast_dup_slice'd?
    (patterns, buf_len(patterns)), new_stmt_list(pos, (&stmt, 1)),
  );
}

def parse_stmt_switch(pos: SrcPos) -> &Stmt {
  let mut expr: &Expr = parse_paren_expr();
  let mut cases: *SwitchCase;
  expect_token(TokenKind.Lbrace);
  while (!is_token_eof() && !is_token(TokenKind.Rbrace)) {
    let mut case_stmt = parse_stmt_switch_case();
    buf_push(&cases as &&void, &case_stmt, sizeof(case_stmt));
  }
  expect_token(TokenKind.Rbrace);
  return new_stmt_switch(pos, expr, (cases, buf_len(cases)));
}

def parse_stmt() -> &Stmt {
  let mut notes = parse_notes();
  let mut pos = token.pos;
  let mut stmt: &Stmt;
  if (match_keyword(if_keyword)) {
    if (match_token(TokenKind.Semicolon)) {
      return new_stmt_close(pos, Stmt.If);
    }
    stmt = parse_stmt_if(pos);
  } else if (match_keyword(while_keyword)) {
    if (match_token(TokenKind.Semicolon)) {
      return new_stmt_close(pos, Stmt.While);
    }
    stmt = parse_stmt_while(pos);
  } else if (match_keyword(do_keyword)) {
    stmt = parse_stmt_do_while(pos);
  } else if (match_keyword(for_keyword)) {
    if (match_token(TokenKind.Semicolon)) {
      return new_stmt_close(pos, Stmt.For);
    }
    stmt = parse_stmt_for(pos);
  } else if (match_keyword(switch_keyword)) {
    if (match_token(TokenKind.Semicolon)) {
      return new_stmt_close(pos, Stmt.Switch);
    }
    stmt = parse_stmt_switch(pos);
  } else if (is_token(TokenKind.Lbrace)) {
    stmt = new_stmt_block(pos, parse_stmt_block());
  } else if (match_keyword(break_keyword)) {
    expect_token(TokenKind.Semicolon);
    stmt = new_stmt_break(pos);
  } else if (match_keyword(continue_keyword)) {
    expect_token(TokenKind.Semicolon);
    stmt = new_stmt_continue(pos);
  } else if (match_keyword(return_keyword)) {
    let mut expr: &Expr;
    if (!is_token(TokenKind.Semicolon)) {
      expr = parse_expr();
    }
    expect_token(TokenKind.Semicolon);
    stmt = new_stmt_return(pos, expr);
  } else if (match_token(TokenKind.Pound)) {
    let mut note: Note = parse_note();
    expect_token(TokenKind.Semicolon);
    stmt = new_stmt_note(pos, note);
  } else if (match_keyword(goto_keyword)) {
    stmt = new_stmt_goto(pos, parse_name());
    expect_token(TokenKind.Semicolon);
  } else {
    stmt = parse_simple_stmt();
    if (stmt.kind != Stmt.Label) {
      expect_token(TokenKind.Semicolon);
    }
  }
  stmt.notes = notes;
  return stmt;
}

def parse_name() -> *const char {
  let mut name = token.name;
  expect_token(TokenKind.Name);
  return name;
}

def parse_decl_enum_item() -> EnumItem {
  let mut pos = token.pos;
  let mut name = parse_name();
  let mut init: &Expr = NULL;
  if (match_token(TokenKind.Assign)) {
    init = parse_expr();
  }
  return (pos, name, init);
}

def parse_decl_enum(pos: SrcPos) -> &Decl {
  let mut name: *const char = NULL;
  if (is_token(TokenKind.Name)) {
    name = parse_name();
  }
  let mut type: &Typespec = NULL;
  if (match_token(TokenKind.Assign)) {
    type = parse_type();
  }
  expect_token(TokenKind.Lbrace);
  let mut items: *EnumItem = NULL;
  while (!is_token(TokenKind.Rbrace)) {
    let mut item = parse_decl_enum_item();
    buf_push(&items as &&void, &item, sizeof(item));
    if (!match_token(TokenKind.Comma)) {
      break;
    }
  }
  expect_token(TokenKind.Rbrace);
  return new_decl_enum(pos, name, type, (items, buf_len(items)));
}

def parse_decl_aggregate_item() -> Member {
  let mut pos = token.pos;
  if (match_keyword(struct_keyword)) {
    return {
      pos: pos,
      kind: Member.Subaggregate,
      subaggregate: parse_aggregate(AggregateKind.Struct, NULL, NULL),
    };
  } else if (match_keyword(union_keyword)) {
    return {
      pos: pos,
      kind: Member.Subaggregate,
      subaggregate: parse_aggregate(AggregateKind.Union, NULL, NULL),
    };
  } else {
    let mut name = parse_name();
    expect_token(TokenKind.Colon);
    let mut type = parse_type();
    expect_token(TokenKind.Semicolon);
    return Member.Field{name, pos, type};
  } if;
}

let mut enum_tag_names = <[]*const char>["kind"];
let mut enum_tag_name_interned = false;

def parse_aggregate(
  kind: AggregateKind, name: *const char, notes: &Slice<Note>,
) -> &Aggregate {
  let mut pos = token.pos;
  let mut items: *Member;
  let mut has_enum = false;
  let mut enum_union: &Aggregate;
  if (match_keyword(switch_keyword)) {
    if (kind != AggregateKind.Union) {
      fatal_error(pos, "Switch aggregate applies only to union");
    }
    items = parse_switch_union();
    has_enum = true;
  } else {
    expect_token(TokenKind.Lbrace);
    while (!is_token_eof() && !is_token(TokenKind.Rbrace)) {
      let mut item = parse_decl_aggregate_item();
      buf_push(&items as &&void, &item, sizeof(item));
      // See if it's a switch union.
      if (item.kind == Member.Subaggregate) {
        let mut sub = item.subaggregate;
        if (sub.kind == AggregateKind.Union && sub.members.length) {
          if (sub.members.items.kind_names.length) {
            // But only one subunion here is allowed.
            if (enum_union) {
              fatal_error(sub.pos, "Multiple switch unions in struct");
              return NULL;
            }
            enum_union = sub;
          } if;
        } if;
      } if;
    } while;
    expect_token(TokenKind.Rbrace);
    if (notes && get_note(notes, enum_keyword)) {
      has_enum = true;
    }
  } if;
  // Check for enum union conditions.
  // TODO Change conditions for enum union. Like has a switch union.
  // TODO Then default to enum union.
  if (name && (has_enum || enum_union)) {
    if (kind == AggregateKind.Union) {
      // This is the union. Contain it.
      enum_union = new_aggregate(pos, kind, (items, buf_len(items)));
      let mut subitem = Member {
        pos: pos,
        kind: Member.Subaggregate,
        subaggregate: enum_union,
      };
      // Wrap it in a struct.
      kind = AggregateKind.Struct;
      items = NULL;
      buf_push(&items as &&void, &subitem, sizeof(subitem));
    } else if (!enum_union) {
      enum_union = get_subunion((items, buf_len(items)));
      if (!enum_union) {
        fatal_error(pos, "No union for enum tag");
        return NULL;
      }
    }
    // Insert the new kind member to the struct at the top.
    // TODO Do we know up front to avoid this after the fact insertion?
    if (!enum_tag_name_interned) {
      enum_tag_names[0] = str_intern(enum_tag_names[0]);
      enum_tag_name_interned = true;
    }
    let mut tag_type_name = build_scoped_name(name, "Kind");
    let mut tag_item = Member{
      pos,
      kind: Member.Field,
      name: *enum_tag_names,
      type: new_typespec_name1(pos, tag_type_name),
    };
    buf_unshift(&items as &&void, &tag_item, sizeof(tag_item));
    // Create and attach union enum.
    // TODO Then use that elsewhere instead of having to recheck notes.
    build_enum_union_decl(enum_union, name);
  } if;
  let mut result = new_aggregate(pos, kind, (items, buf_len(items)));
  if (enum_union) {
    // Remember this at top for easy access later.
    result.union_enum_decl = enum_union.union_enum_decl;
  }
  return result;
}

let mut empty_names = <[]*const char>[""];

def parse_switch_union() -> *Member {
  expect_token(TokenKind.Lbrace);
  let mut items: *Member;
  while (!is_token_eof() && !is_token(TokenKind.Rbrace)) {
    let mut kind_names: **const char;
    let mut kind_name = parse_name();
    buf_push(&kind_names as &&void, &kind_name, sizeof(kind_name));
    // TODO Ellipse ranges.
    while (match_token(TokenKind.Comma)) {
      kind_name = parse_name();
      buf_push(&kind_names as &&void, &kind_name, sizeof(kind_name));
    }
    expect_token(TokenKind.Spear);
    let mut pos = token.pos;
    let mut item: Member;
    if (match_token(TokenKind.Colon)) {
      let mut type = parse_type();
      if (!(
        type.kind == Typespec.Name &&
        type.names.length == 1 &&
        type.names[0].name == void_name
      )) {
        fatal_error(token.pos, "Anonymous switch fields can only be void");
        return NULL;
      }
      expect_token(TokenKind.Semicolon);
      item = Member.Field{pos, name: *empty_names, type};
    } else {
      item = parse_decl_aggregate_item();
    }
    item.kind_names = (kind_names, buf_len(kind_names));
    buf_push(&items as &&void, &item, sizeof(item));
  } while;
  expect_token(TokenKind.Rbrace);
  return items;
}

def build_enum_union_decl(enum_union: &Aggregate, decl_name: *const char) {
  // TODO Extract function.
  // Lowercase enum because we can't have a union member named enum, at least
  // not without escaping, so this uses a less likely collision, even if it's
  // a lowercase typename.
  let mut enum_type_name = build_scoped_name(decl_name, "Kind");
  // Figure out how many items we really have.
  let mut num_all_items: usize = 0;
  for &(enum_union.members) do(union_member) {
    let mut num_names = union_member.kind_names.length;
    if (!num_names) {
      // Non-switch enum union.
      if (union_member.kind != Member.Field) {
        fatal_error(
          union_member.pos, "Enum union item of %s not a field", decl_name,
        );
        return;
      }
      num_names = 1;
    } if;
    num_all_items += num_names;
  } for;
  // Now build the enum items.
  let mut enum_items = xmalloc(num_all_items * sizeof(EnumItem)) as *EnumItem;
  let mut enum_item_index: usize = 0;
  for &(enum_union.members) do(union_member) {
    let mut names = union_member.kind_names;
    if (!names.length) {
      // Non switch.
      names = (&union_member.name, 1);
    }
    for (names) do(name) {
      enum_items[enum_item_index++] = EnumItem{name, union_member.pos};
    }
  } for;
  // And add the new enum.
  let mut union_enum_decl = new_decl_enum(
    enum_union.pos, enum_type_name, NULL, (enum_items, num_all_items),
  );
  enum_union.union_enum_decl = union_enum_decl;
}

def parse_type_params() -> Slice<Decl> {
  let mut params: Slice<Decl>;
  if (match_token(TokenKind.Lt)) {
    do {
      let mut param_name = parse_name();
      let mut constraint: &Typespec;
      if (match_token(TokenKind.Colon)) {
        constraint = parse_type();
      }
      let mut param = Decl {
        kind: Decl.Typedef,
        pos: token.pos,
        is_generic: true,
        name: param_name,
        typedef_decl: {constraint},
        // let mut val default to null.
      };
      buf_push(&params.items as &&void, &param, sizeof(param));
    } while (match_token(TokenKind.Comma));
    expect_token(TokenKind.Gt);
    params.length = buf_len(params.items);
  }
  return params;
}

def parse_decl_aggregate(
  pos: SrcPos, kind: Decl.Kind, notes: &Slice<Note>,
) -> &Decl {
  #assert(kind == Decl.Struct || kind == Decl.Union);
  let mut name = parse_name();
  let mut aggregate_kind =
    kind == Decl.Struct ? AggregateKind.Struct : AggregateKind.Union;
  let mut params = parse_type_params();
  let mut decl: &Decl;
  if (match_token(TokenKind.Semicolon)) {
    // TODO Need to allow switch here, too, for union switches.
    decl = new_decl_aggregate(
      pos, kind, name, params, new_aggregate(pos, aggregate_kind, {}),
    );
    decl.is_incomplete = true;
    return decl;
  } else {
    let mut aggregate = parse_aggregate(aggregate_kind, name, notes);
    // Map back again since this might have changed due to union switch.
    kind = aggregate.kind == AggregateKind.Struct ? Decl.Struct : Decl.Union;
    decl = new_decl_aggregate(pos, kind, name, params, aggregate);
  }
  if (decl.aggregate.union_enum_decl) {
    // Back reference to aggregate.
    decl.aggregate.union_enum_decl.enum_decl.kind_aggregate_decl = decl;
  }
  if (params.length) {
    decl.is_generic = true;
  }
  return decl;
}

def parse_decl_var(pos: SrcPos) -> &Decl {
  let mut is_mut = match_keyword(mut_keyword);
  let mut name = parse_name();
  if (match_token(TokenKind.Assign)) {
    let mut expr = parse_expr();
    expect_token(TokenKind.Semicolon);
    return new_decl_var(pos, name, NULL, expr);
  } else if (match_token(TokenKind.Colon)) {
    let mut type = parse_type();
    let mut expr: &Expr = NULL;
    if (match_token(TokenKind.Assign)) {
      expr = parse_expr();
    }
    expect_token(TokenKind.Semicolon);
    return new_decl_var(pos, name, type, expr);
  } else {
    fatal_error(token.pos, "Expected : or = after let mut, got %s", token_info());
    return NULL;
  }
}

def parse_decl_const(pos: SrcPos) -> &Decl {
  let mut name = parse_name();
  let mut type: &Typespec = NULL;
  if (match_token(TokenKind.Colon)) {
    type = parse_type();
  }
  expect_token(TokenKind.Assign);
  let mut expr = parse_expr();
  expect_token(TokenKind.Semicolon);
  return new_decl_const(pos, name, type, expr);
}

def parse_decl_typedef(pos: SrcPos) -> &Decl {
  let mut name = parse_name();
  expect_token(TokenKind.Assign);
  let mut type = parse_type();
  expect_token(TokenKind.Semicolon);
  return new_decl_typedef(pos, name, type);
}

def parse_decl_func_param(optional_types: bool) -> Member {
  let mut pos = token.pos;
  let mut is_mut = match_keyword(mut_keyword);
  let mut name = parse_name();
  // TODO Default args.
  let mut type: &Typespec;
  if (optional_types) {
    if (match_token(TokenKind.Colon)) {
      type = parse_type();
    }
  } else {
    expect_token(TokenKind.Colon);
    type = parse_type();
  }
  if (is_mut) {
    if (!type) {
      // TODO Check for places that infer type and make sure this is the same!
      type = new_typespec(Typespec.None, pos);
    }
    type.is_mut = true;
  }
  return {name, pos, type};
}

def parse_decl_func_params(
  has_varargs: &bool, optional_types: bool,
) -> Slice<Member> {
  expect_token(TokenKind.Lparen);
  let mut params: *Member = NULL;
  *has_varargs = false;
  while (!is_token(TokenKind.Rparen)) {
    if (match_token(TokenKind.Ellipsis)) {
      if (*has_varargs) {
        error(token.pos, "Multiple ellipsis in function declaration");
      }
      *has_varargs = true;
    } else {
      if (*has_varargs) {
        error(
          token.pos,
          "Ellipsis must be last parameter in function declaration",
        );
      }
      let mut param = parse_decl_func_param(optional_types);
      buf_push(&params as &&void, &param, sizeof(param));
    }
    if (!match_token(TokenKind.Comma)) {
      break;
    }
  } while;
  expect_token(TokenKind.Rparen);
  // Done.
  return (params, buf_len(params));
}

def parse_decl_func(pos: SrcPos) -> &Decl {
  let mut name = parse_name();
  let mut type_params = parse_type_params();
  let mut has_varargs: bool = false;
  let mut params = parse_decl_func_params(&has_varargs, false);
  let mut ret_type: &Typespec = NULL;
  if (match_token(TokenKind.Arrow)) {
    ret_type = parse_type();
  }
  let mut block: StmtList;
  let mut is_incomplete: bool;
  if (match_token(TokenKind.Semicolon)) {
    is_incomplete = true;
  } else {
    block = parse_stmt_block();
    is_incomplete = false;
  }
  let mut decl =
    new_decl_func(pos, name, type_params, params, ret_type, has_varargs, block);
  decl.is_incomplete = is_incomplete;
  if (type_params.length) {
    decl.is_generic = true;
  }
  return decl;
}

def parse_note_arg() -> NoteArg {
  let mut pos = token.pos;
  let mut expr = parse_expr();
  let mut name: *const char = NULL;
  if (match_token(TokenKind.Assign)) {
    if (expr.kind != Expr.Name) {
      fatal_error(token.pos, "Left of: operand = in note argument must be a name");
    }
    name = expr.name;
    expr = parse_expr();
  }
  return {expr, name, pos};
}

def parse_note() -> Note {
  let mut pos = token.pos;
  let mut name: *const char;
  if (is_token(TokenKind.Keyword)) {
    name = token.name;
    next_token();
  } else {
    name = parse_name();
  }
  let mut args: *NoteArg = NULL;
  if (match_token(TokenKind.Lparen)) {
    let mut arg = parse_note_arg();
    buf_push(&args as &&void, &arg, sizeof(arg));
    while (match_token(TokenKind.Comma)) {
      arg = parse_note_arg();
      buf_push(&args as &&void, &arg, sizeof(arg));
    }
    expect_token(TokenKind.Rparen);
  } if;
  return new_note(pos, name, (args, buf_len(args)));
}

def parse_notes() -> Slice<Note> {
  let mut notes: *Note = NULL;
  while (match_token(TokenKind.At)) {
    let mut note = parse_note();
    buf_push(&notes as &&void, &note, sizeof(note));
  }
  return new_notes((notes, buf_len(notes)));
}

def parse_decl_note(pos: SrcPos) -> &Decl {
  return new_decl_note(pos, parse_note());
}

def parse_decl_import(pos: SrcPos) -> &Decl {
  let mut rename_name: *const char;
  let mut is_relative: bool;
  repeat:
  is_relative = false;
  if (match_token(TokenKind.Dot)) {
    is_relative = true;
  }
  let mut name = token.name;
  expect_token(TokenKind.Name);
  if (!is_relative && match_token(TokenKind.Assign)) {
    if (rename_name) {
      fatal_error(pos, "Only one import assignment is allowed");
    }
    rename_name = name;
    goto repeat;
  }
  let mut names: **const char = NULL;
  buf_push(&names as &&void, &name, sizeof(name));
  while (match_token(TokenKind.Dot)) {
    buf_push(&names as &&void, &token.name, sizeof(token.name));
    expect_token(TokenKind.Name);
  }
  let mut import_all = false;
  let mut items: *ImportItem;
  if (match_token(TokenKind.Lbrace)) {
    while (!is_token(TokenKind.Rbrace)) {
      if (match_token(TokenKind.Ellipsis)) {
        import_all = true;
      } else {
        let mut item_name = parse_name();
        if (match_token(TokenKind.Assign)) {
          let mut item = ImportItem {name: parse_name(), rename: item_name};
          buf_push(&items as &&void, &item, sizeof(item));
        } else {
          let mut item = ImportItem {name: item_name};
          buf_push(&items as &&void, &item, sizeof(item));
        }
        if (!match_token(TokenKind.Comma)) {
          break;
        }
      } if;
    } while;
    expect_token(TokenKind.Rbrace);
  } if;
  return new_decl_import(
    pos, rename_name, is_relative,
    (names, buf_len(names)), import_all, (items, buf_len(items)),
  );
}

def parse_decl_opt(notes: &Slice<Note>) -> &Decl {
  let mut pos = token.pos;
  if (match_keyword(enum_keyword)) {
    return parse_decl_enum(pos);
  } else if (match_keyword(struct_keyword)) {
    return parse_decl_aggregate(pos, Decl.Struct, notes);
  } else if (match_keyword(union_keyword)) {
    return parse_decl_aggregate(pos, Decl.Union, notes);
  } else if (match_keyword(const_keyword)) {
    return parse_decl_const(pos);
  } else if (match_keyword(typedef_keyword)) {
    return parse_decl_typedef(pos);
  } else if (match_keyword(def_keyword)) {
    return parse_decl_func(pos);
  } else if (match_keyword(let_keyword)) {
    return parse_decl_var(pos);
  } else if (match_keyword(import_keyword)) {
    return parse_decl_import(pos);
  } else if (match_token(TokenKind.Pound)) {
    return parse_decl_note(pos);
  } else {
    return NULL;
  }
}

def parse_decl() -> &Decl {
  let mut notes = parse_notes();
  let mut decl: &Decl = parse_decl_opt(&notes);
  if (!decl) {
    fatal_error(token.pos, "Expected declaration keyword, got %s", token_info());
  }
  decl.notes = notes;
  return decl;
}

def parse_decls() -> &Slice<&Decl> {
  let mut decls: *&Decl = NULL;
  while (!is_token(TokenKind.Eof)) {
    let mut decl = parse_decl();
    buf_push(&decls as &&void, &decl, sizeof(decl));
  }
  return new_decls((decls, buf_len(decls)));
}
