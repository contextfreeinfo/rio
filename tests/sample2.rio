use "c"

do
  # Assignment here declares new *const* values
  name = "Alice"
  age = 40
  show_prices([
    # Shorthand field names work like JS or Rust
    {age, name},
    {name = "Bernie", age = 14},
    {name = "Clara", age = 9},
    {name = "Darlene", age = 68},
  ])
end

Person struct
  name: string
  age: int
end

show_prices proc(persons: [Person])
  for person in persons
    printf("%s pays $%.2f\n", person.name, ticket_price(person.age))
  end
end

ticket_price proc(age: int): float
  if
    case age <= 11
      6.75
    end
    case age >= 62
      7.00
    end
    else
      if age < 18
        printf("You think age %d gets child prices here?\n", age)
      end
      11.00
    end
  end
end
